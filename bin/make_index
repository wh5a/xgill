#!/usr/bin/perl

# Sixgill: Static assertion checker for C/C++ programs.
# Copyright (C) 2009-2010  Stanford University
# Author: Brian Hackett
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# make the index and all reports for a kind of assertion.

use strict;
use File::Basename;
use File::Copy;

my $usage = "USAGE: make_index kind [diff-dir]\n";
my $assertkind = shift or die $usage;
my $diffdir = shift or "";

# start with a fresh output directory.
system("rm -rf $assertkind") if (-d $assertkind);
mkdir($assertkind);

# get all reports of the kind we are interested in.
my @base_reports = `xdbkeys report_$assertkind.xdb` or die;

# build the final set of reports we'll be generating. this is the same
# as base_reports unless we are diffing two runs.
my @reports;

my %old_hash;
if ($diffdir ne "" && -e "$diffdir/report_$assertkind.xdb") {
    my @old_reports = `xdbkeys $diffdir/report_$assertkind.xdb`;
    foreach my $report (@old_reports) {
        chomp $report;
        $old_hash{$report} = 1;
    }
}

foreach my $report (@base_reports) {
    chomp $report;
    push(@reports, $report) if ($old_hash{$report} != 1);
}

my $report_count = scalar (@reports);
print "Writing $report_count reports...\n";

# map from each file to the array of reports in that file
my %file_hash;

# map from each node in the tree to the directories/files in that node
my %subdir_hash;

# count for each node in the tree of the total number of reports
my %count_hash;

foreach my $report (@reports)
{
    $report =~ /(.*?)\$(.*?)\$(.*)/ or die;
    my $kind = $1;
    my $file = $2;

    die "Mismatched report kind: $kind\n" unless ($assertkind eq $kind);

    push(@{$file_hash{$file}}, $report);

    my $node = $file;
    while ($node ne "") {
        $count_hash{$node}++;
        my $dirname = dirname($node);

        # if the file was relative (usually the case), treat the base
        # directory as the root directory.
        $dirname = "" if ($dirname eq ".");
        $dirname = "" if ($dirname eq "/");

        # add the node as a subdir if we didn't already have it.
        my $found = 0;
        for my $subdir (@{$subdir_hash{$dirname}}) {
            $found = 1 if ($subdir eq $node);
        }
        push(@{$subdir_hash{$dirname}}, $node) if (!$found);

        $node = $dirname;
    }
}

my $title = "Reports: $assertkind";
$title .= " [diff]" if ($diffdir ne "");

open(INDEX, "> $assertkind/index.html");

print INDEX <<EOF;

<html>
<head>

<style type="text/css">

a.dir {
    white-space: pre;
    font-family: monospace;
    text-decoration: none;
    color: black;
}

a.report {
    white-space: pre;
    font-weight: bold;
    text-decoration: none;
    color: blue;
}

span.errors {
    white-space: pre;
    font-family: monospace;
    color: red;
}

</style>

<script type="text/javascript">

function toggleDir(name)
{
    var dirItem = document.getElementById(name);

    if (dirItem.style.display == "none") {
        dirItem.style.display = "";
    }
    else {
        dirItem.style.display = "none";
    }

    return false;
}

</script>

EOF

print INDEX "<title>$title</title>\n";
print INDEX "</head>\n<body>\n";
print INDEX "<h2>$title</h2>\n";

my %next_hash;
my %prev_hash;

my $last_name = "";

recursive_traverse("", 0);

sub recursive_traverse {
    my ($node, $padding) = @_;

    # replace non-alphanumeric chars in the pathname with '_' to get the node ID.
    my $nodeid = $node;
    $nodeid =~ s/[^a-zA-Z0-9]/_/g;

    # if this has a single subdirectory, recurse directly on that subdirectory
    # and don't print anything here.
    if (defined $subdir_hash{$node}) {
        my @subdirs = sort @{$subdir_hash{$node}};
        if (scalar(@subdirs) == 1) {
            my $subdir = pop @subdirs or die;
            recursive_traverse($subdir, $padding);
            return;
        }
    }

    # skip printing anything for the top level directory.
    if ($node ne "") {
        my $count = $count_hash{$node};
        my $errors = (" " x $padding) . "$count";

        # pad the string so that there are always 5 chars
        # for the count and the filenames themselves are aligned.
        $errors .= " " x ($padding + 5 - length($errors));

        print INDEX "<span class='errors'>$errors</span>";
        print INDEX "<a href='#' class='dir' onClick='return toggleDir(\"" . $nodeid . "\")'>$node</a><br>\n";
        print INDEX "<div id='" . $nodeid . "' style='display:none'>\n";
    }

    if (defined $subdir_hash{$node}) {
        my @subdirs = sort @{$subdir_hash{$node}};
        for my $subdir (@subdirs) {
            recursive_traverse($subdir, $padding + 2);
        }
    }

    if (defined $file_hash{$node}) {
        my @file_reports = sort @{$file_hash{$node}};

        # figure out which names are duplicates. these will be adjacent to each other
        # in the sorted list of reports.
        my %duplicates;
        my $last = "";
        for my $report (@file_reports) {
            my $namestr = report_printable($report);
            $duplicates{$namestr} = 1 if ($namestr eq $last);
            $last = $namestr;
        }

        $last = "";
        my $last_count = 0;

        for my $report (@file_reports) {
            my $nameid = report_file($report);
            my $namestr = report_printable($report);

            if ($last eq $namestr) {
                $last_count++;
                $namestr .= " #$last_count";
            }
            else {
                $last = $namestr;
                $last_count = 1;
                $namestr .= " #1" if ($duplicates{$namestr} == 1);
            }

            my $padstr = " " x ($padding + 2);

            if ($last_name ne "") {
                $next_hash{$last_name} = $nameid;
                $prev_hash{$nameid} = $last_name;
            }
            $last_name = $nameid;

            $namestr = html_escape($namestr);

            print INDEX "<span class='errors'>$padstr</span>";
            print INDEX "<a href='$nameid.html' class='report'>$namestr</a><br>\n";
        }
    }

    if ($node ne "") {
        print INDEX "</div>\n";
    }
}

print INDEX "</body>\n";
print INDEX "</html>\n";

close INDEX;

print "Wrote $assertkind/index.html\n";

# fork off some processes so we can construct the index in parallel.
# constructing the index is disk bound so this should be advantageous
# even with just a single core. this is setup to use four processes,
# which each handle a different band of the reports.

# child pid, if there is one.
my $pid;

defined($pid = fork) or die "Can't fork";
if (!$pid) {
    shift @reports;
    defined ($pid = fork) or die "Can't fork";
    if (!$pid) {
        shift @reports;
        defined ($pid = fork) or die "Can't fork";
        if (!$pid) {
            shift @reports;
        }
    }
}

my $report = shift @reports;
while ($report)
{
    my $nameid = report_file($report);
    my $output = "$assertkind/$nameid.html";

    # check for a name collision
    die "Duplicate output files: $nameid\n" if (-e $output);

    my $prevlink = $prev_hash{$nameid};
    my $nextlink = $next_hash{$nameid};

    $prevlink = "none" if ($prevlink eq "");
    $nextlink = "none" if ($nextlink eq "");

    my $retcode = system("xdbfind -plain-text report_$assertkind.xdb '$report' | make_report $nameid $prevlink $nextlink > $output");

    if ($retcode ne 0) {
        print "Failed on '$report'\n";
        die;
    }

    print("Wrote report to $output\n");

    # advance to the next report in our band. shift once for each process.
    $report = shift @reports;
    $report = shift @reports;
    $report = shift @reports;
    $report = shift @reports;
}

# wait for any child process to finish.
waitpid($pid, 0) if ($pid);

# get the printable version of a report, stripping file and assert
# identifier info. this is duplicated in make_report, change these together.
sub report_printable {
    my ($report) = @_;

    # remove assertion kind and file information.
    $report =~ /(.*?)\$(.*?)\$(.*)/ or die;
    my $name = $3;

    # report names look like 'name$loop?$assert' or 'file:name$loop?$assert'
    # where the name can look like:
    # foo(arg_types)       (Elsa frontend)
    # foo                  (GCC frontend with C linkage)
    # type foo(arg_types)  (GCC frontend with C++ linkage)

    # eat everything after a '$'.
    $name =~ s/\$.*$//;

    # eat everything after a '(' - argument types. watch out for '(!'
    # which shows up in some GCC template instantiations.
    $name =~ s/\([^\!].*$//;

    # eat some keywords that can appear before the return type.
    $name =~ s/^static //;
    $name =~ s/^virtual //;
    $name =~ s/^const //;

    # eat everything up to a ' ' - return type.
    $name =~ s/[^ <]* //;

    # eat a leading 'file:', but preserve 'class::'
    $name =~ s/^[^:\(]*:([^:])/$1/;

    return $name;
}

# get the name of the file used to print the specified report.
sub report_file {
    my ($report) = @_;

    # use the printable name of the report followed by a hash of
    # the whole string. if we don't use the hash we run into
    # file length restrictions.
    my $namestr = report_printable($report);
    $namestr =~ s/[^a-zA-Z0-9]/_/g;

    my $hash = string_hash($report);
    $namestr .= "_$hash";

    return $namestr;
}

# get a hashcode from a string. stolen from:
# http://poeticcode.wordpress.com/2008/03/10/perl-string-hashcode/
sub string_hash {
    my $hash = 0;
    use integer;
    foreach(split //,shift) {
        $hash = 31*$hash+ord($_);
    }
    return abs($hash);
}

# shared with make_report
sub html_escape {
    my ($text) = @_;

    $text =~ s/\&/\&amp;/g;
    $text =~ s/\</\&lt;/g;
    $text =~ s/\>/\&gt;/g;

    return $text;
}
