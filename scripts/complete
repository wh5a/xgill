#!/usr/bin/perl

# Sixgill: Static assertion checker for C/C++ programs.
# Copyright (C) 2009-2010  Stanford University
# Author: Brian Hackett
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# do a complete run of the system from raw source to reports. this requires
# various xmonitor processes to be running in the background (maybe on other
# machines) and watching a shared poll_file for jobs.

use strict;
use IO::Handle;

#################################
# environment specific settings #
#################################

# directory to build the project from.
my $build_dir = "/path/to/project";
die "Need build directory: $build_dir\n" if (not -d $build_dir);

# location of the build_xgill script to run.
my $build_script = "/path/to/build_xgill";
die "Need build script: $build_script\n" if (not -e $build_script);

# analysis commands with timeouts to use.
my $xmemlocal = "xmemlocal -timeout=20";
my $xinfer = "xinfer -timeout=60";
my $xcheck = "xcheck -timeout=30";

# output directory to do a diff against. leave as "" for no diffing.
my $old_dir = "";
die "Need old directory: $old_dir\n" if ($old_dir ne "" && not -d $old_dir);

##########################
# general purpose script #
##########################

my $usage = "USAGE: xcomplete poll-file result-dir\n";
my $poll_file = shift or die $usage;
my $result_dir = shift or die $usage;

my $pid = fork();
if ($pid != 0) {
    print "Forked, exiting...\n";
    exit(0);
}

mkdir $result_dir if (not -d $result_dir);

open(OUT, "> $result_dir/xcomplete.log");
OUT->autoflush(1);  # don't buffer writes to the main log

# if you only want to do a partial run, comment out the run_ commands here
# which you don't want to do.

chdir $build_dir;
run_frontend();

chdir $result_dir;

run_pass("xmemlocal", $xmemlocal);
run_pass("xinfer", $xinfer);
run_pass("xcheck_annot", "$xcheck -check-kind=annotation");
run_pass("xcheck_rtime", "$xcheck -check-kind=annotation_runtime");
run_pass("xcheck_oflow", "$xcheck -check-kind=write_overflow");

run_index("index_annot", "annotation");
run_index("index_rtime", "annotation_runtime");
run_index("index_oflow", "write_overflow");
archive_indexes();

close(OUT);

sub run_frontend
{
    print OUT "build started: ";
    print OUT scalar(localtime());
    print OUT "\n";

    my $build_log = "$result_dir/build.log";
    my $build_err = "$result_dir/build_err.log";
    system("$build_script $result_dir > $build_log 2> $build_err");

    print OUT "build finished: ";
    print OUT scalar(localtime());
    print OUT "\n";
}

sub run_pass
{
    my ($name, $command) = @_;
    my $log_file = "$result_dir/manager.$name.log";

    # fork off a manager process for the analysis.
    defined(my $pid = fork) or die;

    if (!$pid) {
        # this is the child process, start the manager.
        exec("xmanager > $log_file");
    }

    # can only get here in the parent process. figure out the address of the
    # manager for connecting workers. we need to read the manager's log file,
    # will be the first line. give the manager one second to start up,
    # any longer and something's broken.
    sleep(1);
    my $log_data = `cat $log_file`;
    $log_data =~ /Listening on ([\.\:0-9]*)/ or die;
    my $address = $1;

    # write the configuration file.
    open(POLL, "> $poll_file");
    print POLL "$command\n";
    print POLL "$result_dir/$name\n";
    print POLL "$address\n";
    close(POLL);

    print OUT "$name started: ";
    print OUT scalar(localtime());
    print OUT "\n";

    waitpid($pid, 0);
    unlink($poll_file);

    print OUT "$name finished: ";
    print OUT scalar(localtime());
    print OUT "\n";

    # collate the worker's output into a single file. wait a few seconds first
    # to make sure we get all the worker output.
    sleep(10);
    system("cat $name.*.log > $name.log");
}

# the names of all directories containing reports to archive.
my $indexes;

sub run_index
{
    my ($name, $kind) = @_;

    return if (not (-e "report_$kind.xdb"));

    print OUT "$name started: ";
    print OUT scalar(localtime());
    print OUT "\n";

    # make an index for the report diff if applicable.
    if ($old_dir ne "") {
        system("make_index $kind $old_dir > $name.diff.log");
        system("mv $kind diff_$kind");
        $indexes .= " diff_$kind";
    }

    # make an index for the full set of reports.
    system("make_index $kind > $name.log");
    $indexes .= " $kind";

    print OUT "$name finished: ";
    print OUT scalar(localtime());
    print OUT "\n";
}

sub archive_indexes
{
    print OUT "archive started: ";
    print OUT scalar(localtime());
    print OUT "\n";

    system("tar -czf reports.tgz $indexes");
    system("rm -rf $indexes");

    print OUT "archive finished: ";
    print OUT scalar(localtime());
    print OUT "\n";
}
